////////////////////////////////////////////////////////////////////////////////
//
// PLUGIN_VERSION is the common version name when describing the plugin.
// ATAK_VERSION   is for the version of ATAK this plugin should be compatible
//                with some examples include 3.11.0, 3.11.0.civ 3.11.1.fvey
//
////////////////////////////////////////////////////////////////////////////////

buildscript {

    ext.PLUGIN_VERSION = "1.0.2"
    ext.ATAK_VERSION = "4.10.0"

    def takdevVersion = '2.+'

    def getValueFromPropertiesFile = { propFile, key ->
        if(!propFile.isFile() || !propFile.canRead())
            return null
        def prop = new Properties()
        def reader = propFile.newReader()
        try {
            prop.load(reader)
        } finally {
            reader.close()
        }
        return prop.get(key)
    }

    def getProperty = { name, defValue ->
        def prop = project.properties[name] ?:
                getValueFromPropertiesFile(project.rootProject.file('local.properties'), name)
        return (null == prop) ? defValue : prop
    }

    def urlKey = 'takrepo.url'

    ext.isDevKitEnabled = { ->
        return getProperty(urlKey, null) != null
    }

    ext.takrepoUrl = getProperty(urlKey, 'https://localhost/')
    ext.takrepoUser = getProperty('takrepo.user', 'invalid')
    ext.takrepoPassword = getProperty('takrepo.password', 'invalid')
    ext.takdevPlugin = getProperty('takdev.plugin', "${rootDir}/app/libs/atak-gradle-takdev.jar")

    repositories {
        google()
        mavenCentral()
        mavenLocal()
        maven {
            url "https://jitpack.io"
        }
        maven {
            url = takrepoUrl
            credentials {
                username = takrepoUser
                password = takrepoPassword
            }
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
        if(isDevKitEnabled()) {
            classpath "com.atakmap.gradle:atak-gradle-takdev:${takdevVersion}"
        } else {
            classpath files(takdevPlugin)
        }
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()
        mavenCentral()
        mavenLocal()
        maven {
            url "https://jitpack.io"
        }
    }
}

apply plugin: 'com.android.application'
apply plugin: 'atak-takdev-plugin'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.gms.google-services'

def supportedFlavors =
        [
                [ name : 'civ', default: true ],
                [ name : 'mil' ],
                [ name : 'aus' ],
                [ name : 'nzl' ],
                [ name : 'prt' ],
                [ name : 'nor' ],
                [ name : 'swe' ],
                [ name : 'gbr' ],
                [ name : 'can' ],
                [ name : 'hun' ],
                [ name : 'bel' ],
                [ name : 'est' ],
                [ name : 'ltu' ],
                [ name : 'ukr' ],
                [ name : 'pol' ],
                [ name : 'nld' ],
                [ name : 'esp' ],
                [ name : 'aeronet' ],
                [ name : 'gov' ],
                [ name : 'natosof' ],
        ]

android {
    compileSdk 34
    ndkVersion '25.2.9519653'
    namespace 'com.sync.tak'
    //flavorDimensions

    def pluginApiFlavor = "CIV"

    bundle {
        storeArchive {
            enable = false
        }
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
        }
    }

    lintOptions {
        checkReleaseBuilds true
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError true
    }

    signingConfigs {
        debug { }
        release { }
    }

    buildTypes {
        debug {
            debuggable true
            matchingFallbacks = ['sdk']
        }
        release {
            minifyEnabled true
            proguardFile 'proguard-gradle.txt'
            signingConfig signingConfigs.release
            matchingFallbacks = ['odk']
        }
    }

    flavorDimensions "application"
    productFlavors {
        supportedFlavors.each { flav ->
            "${flav.name}" {
                getIsDefault().set(flav.name.equals("${flav.default}"))
                dimension "application"

                // retain existing application identifiers for civ and mil
                if (!flav.name.equals("civ") && !flav.name.equals("mil")) {
                    //applicationIdSuffix = ".${flav.name}"
                }
                // resolve CIV dependency if flavor specific dependency is not available
                matchingFallbacks = ['civ']

                // set the api version information
                pluginApiFlavor = flav.name.equals('gov') ? 'CIV' : "${flav.name.toUpperCase()}"
                manifestPlaceholders = [atakApiVersion: "com.atakmap.app@" + ATAK_VERSION + ".CIV"]

                // additional variables associated with the flav can be set here once defined above
                // buildConfigField 'boolean', 'EXAMPLE', "${flav.example}"
            }
        }

        // additional variables associated with all flavors can be set here
        applicationVariants.all { variant ->
            variant.resValue "string", "versionName", variant.versionName
            buildConfigField 'String', 'ATAK_PACKAGE_NAME', '"com.atakmap.app.civ"'
        }
    }

    packagingOptions {
        exclude 'META-INF/INDEX.LIST'

        // Extract native libraries from the APK - required for plugin loading
        jniLibs.useLegacyPackaging true
    }

    sourceSets {
        main {
            // It is strongly encouraged that plugin developers do not modify the archiveBaseName, version code logic and version
            // name logic to provide for consistency within the community.
            setProperty("archivesBaseName", "ATAK-Plugin-" + rootProject.name + "-" + PLUGIN_VERSION + "-" + getVersionName() + "-" + ATAK_VERSION)
            defaultConfig.versionCode = getVersionCode()
            defaultConfig.versionName = PLUGIN_VERSION + " (" + getVersionName() + ") - [" + ATAK_VERSION + "." + /* pluginApiFlavor */ "CIV" + "]"
        }

        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34

        def runTasks = gradle.startParameter.taskNames
        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildFeatures {
        buildConfig = true
    }
}

kotlin {
    jvmToolchain(11)
}

dependencies {
    implementation fileTree(dir: 'libs', include: '*.jar')
    androidTestImplementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.2'

    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.7.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.7.5'
    implementation "androidx.documentfile:documentfile:1.0.1"
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.palette:palette-ktx:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.2.0-alpha01"

    implementation 'com.google.android.material:material:1.12.0-alpha01'
    implementation 'com.google.android.gms:play-services-auth:20.7.0'

    implementation platform('com.google.firebase:firebase-bom:32.6.0')
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.google.firebase:firebase-firestore'

    implementation 'com.android.volley:volley:1.2.1'
    implementation 'com.github.KieronQuinn:MonetCompat:0.4.1'
    implementation 'com.microsoft.design:fluent-system-icons:1.1.223@aar'
}

/*
configurations.configureEach {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group == 'androidx.core') {
            details.useVersion "1.8.0"
        }
        if (details.requested.group == 'androidx.lifecycle') {
            details.useVersion "2.5.1"
        }
        if (details.requested.group == 'androidx.fragment') {
            details.useVersion "1.5.5"
        }
    }
}
configurations.implementation {
    exclude group: 'androidx.core', module: 'core-ktx'
    exclude group: 'androidx.core', module: 'core'
    exclude group: 'androidx.fragment', module: 'fragment'
    exclude group: 'androidx.lifecycle', module: 'lifecycle'
    exclude group: 'androidx.lifecycle', module: 'lifecycle-process'
}*/

